; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{DB15D326-3441-4107-8444-35E75A2ABD7F}
AppName=mwNSPECT
AppVerName=mwNSPECT
AppPublisher=NOAA Coastal Services Center
VersionInfoCompany=NOAA Coastal Services Center
VersionInfoDescription=NOAA Coastal Services Center
VersionInfoTextVersion=1.0
VersionInfoVersion=1.0
AppPublisherURL=http://www.csc.noaa.gov/
AppSupportURL=http://www.csc.noaa.gov/
AppUpdatesURL=http://www.csc.noaa.gov/
DefaultDirName=C:\NSPECT
DefaultGroupName=mwNSPECT
EnableDirDoesntExistWarning=no
OutputBaseFilename=mwNSPECT_Setup
AllowNoIcons=yes
DisableProgramGroupPage=yes
;LicenseFile=License.rtf
Compression=lzma/ultra
SolidCompression=yes
InternalCompressLevel=ultra

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
;Source: "License.rtf"; DestDir: "{app}"; Flags: ignoreversion
Source: "Base Files\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs uninsneveruninstall
Source: "Base Files\bin\mwNSPECT.dll";  Check: GetMWPluginDestination; DestDir: "{code:PluginDestination}"; Flags: ignoreversion overwritereadonly
Source: "Base Files\bin\MapWinGeoProc.dll";  Check: GetMWPluginDestination; DestDir: "{code:PluginDestination}\..\.."; Flags: ignoreversion overwritereadonly

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Dirs]
Name: {app}; Permissions: everyone-modify
Name: {app}\projects; Permissions: everyone-modify
Name: {app}\workspace; Permissions: everyone-modify
Name: {app}\metadata; Permissions: everyone-modify

[Registry]
;Installation Path
Root: HKLM; Subkey: "Software\NOAA Coastal Services Center\mwNSPECT"; ValueType: string; ValueName: InstallPath; ValueData: {app}; Flags: uninsdeletekey deletekey deletevalue

[Types]
Name: Standard_Installation; Description: Standard mwNSPECT installation; Flags: iscustom


[Run]
; Make sure the windows installer gets installed before you try
; to use it below (msiexec)


[Code]

var sPluginDest : String;

//
// Search for the path where MW was installed.  Return true if path found.
// Set variable to plugin folder
//

function GetMWPluginDestination(): Boolean;
var
  i:      Integer;
  len:    Integer;

begin
  sPluginDest := '';

  RegQueryStringValue( HKLM, 'SOFTWARE\Classes\MapWindowConfig\DefaultIcon', '', sPluginDest );
  len := Length(sPluginDest);
  if len > 0 then
  begin
    i := len;
    while sPluginDest[i] <> '\' do
      begin
        i := i-1;
      end;

    i := i+1;
    Delete(sPluginDest, i, Len-i+1);
    Insert('plugins\mwNSPECT\', sPluginDest, i);
  end;
  len := Length(sPluginDest);
  Result := len > 0;
end;

//
//  Use this function to return path to install plugin
//
function PluginDestination(Param: String) : String;
begin
   Result := sPluginDest;
end;

